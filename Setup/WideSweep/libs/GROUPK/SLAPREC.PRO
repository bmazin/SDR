;+
;     - - - - - - - -
;      s l a P R E C
;     - - - - - - - -
;
;  Form the matrix of precession between two epochs (IAU 1976, FK5)
;  (double precision)
;
;  Given:
;     EP0    dp         beginning epoch
;     EP1    dp         ending epoch
;
;  Returned:
;     RMATP  dp(3,3)    precession matrix
;
;  Notes:
;
;     1)  The epochs are TDB (loosely ET) Julian epochs.
;
;     2)  The matrix is in the sense   V(EP1)  =  RMATP * V(EP0)
;
;  References:
;     Lieske,J.H., 1979. Astron.Astrophys.,73,282.
;      equations (6) & (7), p283.
;     Kaplan,G.H., 1981. USNO circular no. 163, pA2.
;
;  Called:  sla_DEULER
;
;  P.T.Wallace   Starlink   12 April 1990
;-
pro slaPREC, EP0, EP1, RMATP

;  Arc seconds to radians
         AS2R=0.4848136811095359949D-05

;  Interval between basic epoch J2000.0 and beginning epoch (JC)
         T0 = (EP0-2000D0)/100D0

;  Interval over which precession required (JC)
         T = (EP1-EP0)/100D0

;  Euler angles
         TAS2R = T*AS2R
         W = 2306.2181D0+(1.39656D0-0.000139D0*T0)*T0

         ZETA = (W+((0.30188D0-0.000344D0*T0)+0.017998D0*T)*T)*TAS2R

         Z = (W+((1.09468D0+0.000066D0*T0)+0.018203D0*T)*T)*TAS2R

         THETA = ((2004.3109D0+(-0.85330D0-0.000217D0*T0)*T0)+$
                 ((-0.42665D0-0.000217D0*T0)-0.041833D0*T)*T)*TAS2R

;  Rotation matrix
         slaDEULER,'ZYZ',-ZETA,THETA,-Z,RMATP

end
